<!DOCTYPE html>
<html>
	<head>
		<meta charset="UTF-8">
		<title>GLSL的核心</title>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/84/three.min.js"></script>
		<style type="text/css">
			.p-canvas-webgl {
			  position: fixed;;
			  z-index: 1;
			  top: 0; left: 0;
			}
		</style>
	</head>
	<body>
		<canvas class="p-canvas-webgl" id="canvas-webgl"></canvas>
		<script id="vs" type="x-shader/x-vertex">
		  attribute vec3 position;
		  attribute vec3 normal;
		  attribute vec3 faceNormal;
		  attribute vec3 center;
		  attribute float delay;
		  
		  uniform mat4 modelViewMatrix;
		  uniform mat4 projectionMatrix;
		  uniform float time;
		  
		  varying vec3 vPosition;
		  varying float vNoise;
		  varying float vNow;
		  
		  const float duration = 2.0;
		  const float delayAll = 1.0;
		  
		  float ease(float t) {
		    return t == 1.0 ? t : 1.0 - pow(2.0, -10.0 * t);
		  }
		  vec3 mod289(vec3 x)
		  {
		    return x - floor(x * (1.0 / 289.0)) * 289.0;
		  }
		  
		  vec4 mod289(vec4 x)
		  {
		    return x - floor(x * (1.0 / 289.0)) * 289.0;
		  }
		  
		  vec4 permute(vec4 x)
		  {
		    return mod289(((x*34.0)+1.0)*x);
		  }
		  
		  vec4 taylorInvSqrt(vec4 r)
		  {
		    return 1.79284291400159 - 0.85373472095314 * r;
		  }
		  
		  vec3 fade(vec3 t) {
		    return t*t*t*(t*(t*6.0-15.0)+10.0);
		  }
		  
		  // Classic Perlin noise
		  float cnoise3(vec3 P)
		  {
		    vec3 Pi0 = floor(P); // Integer part for indexing
		    vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
		    Pi0 = mod289(Pi0);
		    Pi1 = mod289(Pi1);
		    vec3 Pf0 = fract(P); // Fractional part for interpolation
		    vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
		    vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
		    vec4 iy = vec4(Pi0.yy, Pi1.yy);
		    vec4 iz0 = Pi0.zzzz;
		    vec4 iz1 = Pi1.zzzz;
		  
		    vec4 ixy = permute(permute(ix) + iy);
		    vec4 ixy0 = permute(ixy + iz0);
		    vec4 ixy1 = permute(ixy + iz1);
		  
		    vec4 gx0 = ixy0 * (1.0 / 7.0);
		    vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
		    gx0 = fract(gx0);
		    vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
		    vec4 sz0 = step(gz0, vec4(0.0));
		    gx0 -= sz0 * (step(0.0, gx0) - 0.5);
		    gy0 -= sz0 * (step(0.0, gy0) - 0.5);
		  
		    vec4 gx1 = ixy1 * (1.0 / 7.0);
		    vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
		    gx1 = fract(gx1);
		    vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
		    vec4 sz1 = step(gz1, vec4(0.0));
		    gx1 -= sz1 * (step(0.0, gx1) - 0.5);
		    gy1 -= sz1 * (step(0.0, gy1) - 0.5);
		  
		    vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
		    vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
		    vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
		    vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
		    vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
		    vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
		    vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
		    vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
		  
		    vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
		    g000 *= norm0.x;
		    g010 *= norm0.y;
		    g100 *= norm0.z;
		    g110 *= norm0.w;
		    vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
		    g001 *= norm1.x;
		    g011 *= norm1.y;
		    g101 *= norm1.z;
		    g111 *= norm1.w;
		  
		    float n000 = dot(g000, Pf0);
		    float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
		    float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
		    float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
		    float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
		    float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
		    float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
		    float n111 = dot(g111, Pf1);
		  
		    vec3 fade_xyz = fade(Pf0);
		    vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
		    vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
		    float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
		    return 2.2 * n_xyz;
		  }
		  
		  mat4 computeTranslateMat(vec3 v) {
		    return mat4(
		      1.0, 0.0, 0.0, 0.0,
		      0.0, 1.0, 0.0, 0.0,
		      0.0, 0.0, 1.0, 0.0,
		      v.x, v.y, v.z, 1.0
		    );
		  }
		  mat4 computeRotateMatX(float radian) {
		    return mat4(
		      1.0, 0.0, 0.0, 0.0,
		      0.0, cos(radian), -sin(radian), 0.0,
		      0.0, sin(radian), cos(radian), 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMatY(float radian) {
		    return mat4(
		      cos(radian), 0.0, sin(radian), 0.0,
		      0.0, 1.0, 0.0, 0.0,
		      -sin(radian), 0.0, cos(radian), 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMatZ(float radian) {
		    return mat4(
		      cos(radian), -sin(radian), 0.0, 0.0,
		      sin(radian), cos(radian), 0.0, 0.0,
		      0.0, 0.0, 1.0, 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMat(float radX, float radY, float radZ) {
		    return computeRotateMatX(radX) * computeRotateMatY(radY) * computeRotateMatZ(radZ);
		  }
		  
		  void main() {
		    float now = ease(max((time - delayAll - delay - (faceNormal.x + 1.0) / 2.0 - (faceNormal.y + 1.0) / 2.0) / duration, 0.0));
		    mat4 translateMat = computeTranslateMat(vec3(faceNormal) * 1200.0 * (1.0 - now) + vec3(0.0, sin(time) * 10.0, 0.0));
		    mat4 rotateMat = computeRotateMat(0.0, radians((1.0 - now) * faceNormal.y * 4320.0), 0.0);
		    float rotateRadian = radians((time + faceNormal.x + faceNormal.y) * 1440.0);
		    mat4 rotateMatSelf = computeRotateMat(rotateRadian, rotateRadian, 0.0);
		    float noise = smoothstep(-0.4, 0.4,
		      cnoise3(vec3(position.x * 0.035 - time, position.y * 0.035 - time, position.z * 0.035 + time))
		    ) * 2.0 - 1.0;
		    vec3 updatePositionSelf = (rotateMatSelf * vec4(position - center, 1.0)).xyz * (1.0 - now) + position
		      + normalize(position) * noise ;
		    vec4 updatePosition = rotateMat * translateMat * vec4(updatePositionSelf, 1.0);
		    vPosition = updatePosition.xyz;
		    vNoise = noise;
		    vNow = now;
		    gl_Position = projectionMatrix * modelViewMatrix * updatePosition;
		  }
		  
		</script>
		<script id="fs" type="x-shader/x-fragment">
		  precision highp float;
		  
		  uniform mat3 normalMatrix;
		  uniform float time;
		  
		  varying vec3 vPosition;
		  varying float vNoise;
		  varying float vNow;
		  
		  const vec3 lightDirection = vec3(1.0, -1.0, -1.0);
		  const float duration = 2.0;
		  const float delayAll = 1.0;
		  
		  vec3 convertHsvToRgb(vec3 c) {
		    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
		    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
		    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
		  }
		  
		  void main() {
		    float now = clamp((time - delayAll - 1.5) / duration, 0.0, 1.0);
		    vec3 normal = normalize(cross(dFdx(vPosition), dFdy(vPosition)));
		    vec3 light = normalize(normalMatrix * lightDirection);
		    float diff = (dot(normal, light) + 1.0) / 2.0 * 0.2;
		    float opacity = smoothstep(0.1, 0.2, vNow);
		    vec3 v = normalize(vPosition);
		    vec3 rgb = (1.0 - now) * vec3(1.0) + convertHsvToRgb(vec3(0.5 + (v.x + v.y + v.x) / 40.0 + time * 0.1, 0.8, 0.4 + sin(time) * 0.05 + vNoise * 0.02));
		    gl_FragColor = vec4(rgb + diff, opacity);
		  }
		  
		</script>
		<script id="vs-shell" type="x-shader/x-vertex">
		  attribute vec3 position;
		  
		  uniform mat4 modelViewMatrix;
		  uniform mat4 projectionMatrix;
		  uniform float time;
		  
		  varying vec3 vPosition;
		  varying float vOpacity;
		  
		  const float duration = 4.0;
		  const float delay = 3.0;
		  
		  float ease(float t) {
		    return t == 1.0 ? t : 1.0 - pow(2.0, -10.0 * t);
		  }
		  
		  mat4 computeTranslateMat(vec3 v) {
		    return mat4(
		      1.0, 0.0, 0.0, 0.0,
		      0.0, 1.0, 0.0, 0.0,
		      0.0, 0.0, 1.0, 0.0,
		      v.x, v.y, v.z, 1.0
		    );
		  }
		  mat4 computeScaleMat(vec3 scale) {
		    return mat4(
		      scale.x, 0.0, 0.0, 0.0,
		      0.0, scale.y, 0.0, 0.0,
		      0.0, 0.0, scale.z, 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMatX(float radian) {
		    return mat4(
		      1.0, 0.0, 0.0, 0.0,
		      0.0, cos(radian), -sin(radian), 0.0,
		      0.0, sin(radian), cos(radian), 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMatY(float radian) {
		    return mat4(
		      cos(radian), 0.0, sin(radian), 0.0,
		      0.0, 1.0, 0.0, 0.0,
		      -sin(radian), 0.0, cos(radian), 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMatZ(float radian) {
		    return mat4(
		      cos(radian), -sin(radian), 0.0, 0.0,
		      sin(radian), cos(radian), 0.0, 0.0,
		      0.0, 0.0, 1.0, 0.0,
		      0.0, 0.0, 0.0, 1.0
		    );
		  }
		  mat4 computeRotateMat(float radX, float radY, float radZ) {
		    return computeRotateMatX(radX) * computeRotateMatY(radY) * computeRotateMatZ(radZ);
		  }
		  
		  void main() {
		    float now = clamp((time - delay) / duration, 0.0, 1.0);
		    mat4 translateMat = computeTranslateMat(vec3(0.0, sin(time) * 10.0, 0.0));
		    mat4 scaleMat = computeScaleMat(vec3(ease(now) * 0.6 + 0.4 + sin(time * 2.0) * 0.04));
		    mat4 rotateMat = computeRotateMat(radians(45.0), radians(time * 2.0), radians(-time * 2.0));
		    vec4 updatePosition = translateMat * rotateMat * scaleMat * vec4(position, 1.0);
		    vPosition = normalize(position);
		    vOpacity = normalize(updatePosition).z;
		    gl_Position = projectionMatrix * modelViewMatrix * updatePosition;
		  }
		  
		</script>
		<script id="fs-shell" type="x-shader/x-fragment">
  precision highp float;
  
  uniform float time;
  
  varying vec3 vPosition;
  varying float vOpacity;
  
  const float duration = 4.0;
  const float delay = 3.0;
  
  vec3 mod289(vec3 x)
  {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  vec4 mod289(vec4 x)
  {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  vec4 permute(vec4 x)
  {
    return mod289(((x*34.0)+1.0)*x);
  }
  
  vec4 taylorInvSqrt(vec4 r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }
  
  vec3 fade(vec3 t) {
    return t*t*t*(t*(t*6.0-15.0)+10.0);
  }
  
  // Classic Perlin noise
  float cnoise3(vec3 P)
  {
    vec3 Pi0 = floor(P); // Integer part for indexing
    vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
    Pi0 = mod289(Pi0);
    Pi1 = mod289(Pi1);
    vec3 Pf0 = fract(P); // Fractional part for interpolation
    vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
    vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
    vec4 iy = vec4(Pi0.yy, Pi1.yy);
    vec4 iz0 = Pi0.zzzz;
    vec4 iz1 = Pi1.zzzz;
  
    vec4 ixy = permute(permute(ix) + iy);
    vec4 ixy0 = permute(ixy + iz0);
    vec4 ixy1 = permute(ixy + iz1);
  
    vec4 gx0 = ixy0 * (1.0 / 7.0);
    vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
    gx0 = fract(gx0);
    vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
    vec4 sz0 = step(gz0, vec4(0.0));
    gx0 -= sz0 * (step(0.0, gx0) - 0.5);
    gy0 -= sz0 * (step(0.0, gy0) - 0.5);
  
    vec4 gx1 = ixy1 * (1.0 / 7.0);
    vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
    gx1 = fract(gx1);
    vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
    vec4 sz1 = step(gz1, vec4(0.0));
    gx1 -= sz1 * (step(0.0, gx1) - 0.5);
    gy1 -= sz1 * (step(0.0, gy1) - 0.5);
  
    vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
    vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
    vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
    vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
    vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
    vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
    vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
    vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);
  
    vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
    g000 *= norm0.x;
    g010 *= norm0.y;
    g100 *= norm0.z;
    g110 *= norm0.w;
    vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
    g001 *= norm1.x;
    g011 *= norm1.y;
    g101 *= norm1.z;
    g111 *= norm1.w;
  
    float n000 = dot(g000, Pf0);
    float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
    float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
    float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
    float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
    float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
    float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
    float n111 = dot(g111, Pf1);
  
    vec3 fade_xyz = fade(Pf0);
    vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
    vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
    float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x);
    return 2.2 * n_xyz;
  }
  vec3 convertHsvToRgb(vec3 c) {
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
  }
  float random(vec2 c){
    return fract(sin(dot(c.xy ,vec2(12.9898,78.233))) * 43758.5453);
  }
  
  void main() {
    float now = clamp((time - delay) / duration, 0.0, 1.0);
    float noise1 = cnoise3(vec3((vPosition * vec3(0.4, 2.0, -0.6) * 2.0 + time))) * 7.0 - (1.0 + (1.0 - now) * 7.0);
    float noise2 = cnoise3(vec3((vPosition * 42.0 + time)));
    float noise3 = cnoise3(vec3((vPosition * 7.0 + time))) * 2.0;
    float bright = smoothstep(-0.2, 1.0, (noise1 + noise2 + noise3) * now);
    vec3 v = normalize(vPosition);
    vec3 rgb = (1.0 - now) * vec3(1.0) + convertHsvToRgb(vec3(0.5 + (v.x + v.y + v.x) / 40.0 + time * 0.1, 0.4, 1.0));
    float whiteNoise = random(vPosition.xy);
    if (bright < 0.4) discard;
    gl_FragColor = vec4(rgb * vec3(1.0 - bright + 0.6) + whiteNoise * 0.2, 0.4 + vOpacity * 0.5 + sin(time * 2.0) * 0.1);
  }
</script>
	    <script type="text/javascript">
	    	'use strict';

			function _classCallCheck(instance, Constructor) { if (!(instance instanceof Constructor)) { throw new TypeError("Cannot call a class as a function"); } }
			
			var MathEx = {
			  degrees: function degrees(radian) {
			    return radian / Math.PI * 180;
			  },
			  radians: function radians(degree) {
			    return degree * Math.PI / 180;
			  },
			  clamp: function clamp(value, min, max) {
			    return Math.min(Math.max(value, min), max);
			  },
			  mix: function mix(x1, x2, a) {
			    return x1 * (1 - a) + x2 * a;
			  },
			  polar: function polar(radian1, radian2, radius) {
			    return [Math.cos(radian1) * Math.cos(radian2) * radius, Math.sin(radian1) * radius, Math.cos(radian1) * Math.sin(radian2) * radius];
			  }
			};
			
			var debounce = function debounce(callback, duration) {
			  var timer;
			  return function (event) {
			    clearTimeout(timer);
			    timer = setTimeout(function () {
			      callback(event);
			    }, duration);
			  };
			};
			
			var computeFaceNormal = function computeFaceNormal(v0, v1, v2) {
			  var n = [];
			  var v1a = [v1[0] - v0[0], v1[1] - v0[1], v1[2] - v0[2]];
			  var v2a = [v2[0] - v0[0], v2[1] - v0[1], v2[2] - v0[2]];
			  n[0] = v1a[1] * v2a[2] - v1a[2] * v2a[1];
			  n[1] = v1a[2] * v2a[0] - v1a[0] * v2a[2];
			  n[2] = v1a[0] * v2a[1] - v1a[1] * v2a[0];
			  var l = Math.sqrt(n[0] * n[0] + n[1] * n[1] + n[2] * n[2], 2);
			  for (var i = 0; i < n.length; i++) {
			    n[i] = n[i] / l;
			  }
			  return n;
			};
			
			var SkyOctahedron = function () {
			  function SkyOctahedron() {
			    _classCallCheck(this, SkyOctahedron);
			
			    this.uniforms = {
			      time: {
			        type: 'f',
			        value: 0
			      }
			    };
			    this.obj = this.createObj();
			  }
			
			  SkyOctahedron.prototype.createObj = function createObj() {
			    var geometry = new THREE.OctahedronBufferGeometry(90, 4);
			    var positions = geometry.attributes.position.array;
			    var faceNormalsBase = [];
			    var centersBase = [];
			    var delaysBase = [];
			    for (var i = 0; i < positions.length; i += 9) {
			      var n = computeFaceNormal([positions[i + 0], positions[i + 1], positions[i + 2]], [positions[i + 3], positions[i + 4], positions[i + 5]], [positions[i + 6], positions[i + 7], positions[i + 8]]);
			      faceNormalsBase.push(n[0], n[1], n[2], n[0], n[1], n[2], n[0], n[1], n[2]);
			      var c = [(positions[i + 0] + positions[i + 3] + positions[i + 6]) / 3, (positions[i + 1] + positions[i + 4] + positions[i + 7]) / 3, (positions[i + 2] + positions[i + 5] + positions[i + 8]) / 3];
			      var delay = Math.random() * 0.5;
			      centersBase.push(c[0], c[1], c[2], c[0], c[1], c[2], c[0], c[1], c[2]);
			      delaysBase.push(delay, delay, delay);
			    }
			    var faceNormals = new Float32Array(faceNormalsBase);
			    var centers = new Float32Array(centersBase);
			    var delays = new Float32Array(delaysBase);
			    geometry.addAttribute('faceNormal', new THREE.BufferAttribute(faceNormals, 3));
			    geometry.addAttribute('center', new THREE.BufferAttribute(centers, 3));
			    geometry.addAttribute('delay', new THREE.BufferAttribute(delays, 1));
			    return new THREE.Mesh(geometry, new THREE.RawShaderMaterial({
			      uniforms: this.uniforms,
			      vertexShader: document.getElementById('vs').textContent,
			      fragmentShader: document.getElementById('fs').textContent,
			      shading: THREE.FlatShading,
			      transparent: true,
			      side: THREE.DoubleSide
			    }));
			  };
			
			  SkyOctahedron.prototype.render = function render(time) {
			    this.uniforms.time.value += time;
			  };
			
			  return SkyOctahedron;
			}();
			
			var SkyOctahedronShell = function () {
			  function SkyOctahedronShell() {
			    _classCallCheck(this, SkyOctahedronShell);
			
			    this.uniforms = {
			      time: {
			        type: 'f',
			        value: 0
			      }
			    };
			    this.obj = this.createObj();
			  }
			
			  SkyOctahedronShell.prototype.createObj = function createObj() {
			    var geometry = new THREE.OctahedronBufferGeometry(150, 4);
			    return new THREE.Mesh(geometry, new THREE.RawShaderMaterial({
			      uniforms: this.uniforms,
			      vertexShader: document.getElementById('vs-shell').textContent,
			      fragmentShader: document.getElementById('fs-shell').textContent,
			      transparent: true,
			      side: THREE.DoubleSide,
			      depthWrite: false
			    }));
			  };
			
			  SkyOctahedronShell.prototype.render = function render(time) {
			    this.uniforms.time.value += time;
			  };
			
			  return SkyOctahedronShell;
			}();
			
			var canvas = document.getElementById('canvas-webgl');
			var renderer = new THREE.WebGLRenderer({
			  antialias: false,
			  canvas: canvas
			});
			var scene = new THREE.Scene();
			var camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 1, 10000);
			var clock = new THREE.Clock();
			
			var skyOctahedron = new SkyOctahedron();
			var skyOctahedronShell = new SkyOctahedronShell();
			
			var resizeWindow = function resizeWindow() {
			  canvas.width = window.innerWidth;
			  canvas.height = window.innerHeight;
			  camera.aspect = window.innerWidth / window.innerHeight;
			  camera.updateProjectionMatrix();
			  renderer.setSize(window.innerWidth, window.innerHeight);
			};
			var render = function render() {
			  var time = clock.getDelta();
			  skyOctahedron.render(time);
			  skyOctahedronShell.render(time);
			  renderer.render(scene, camera);
			};
			var renderLoop = function renderLoop() {
			  render();
			  requestAnimationFrame(renderLoop);
			};
			var on = function on() {
			  window.addEventListener('resize', debounce(function () {
			    resizeWindow();
			  }), 1000);
			};
			
			var init = function init() {
			  renderer.setSize(window.innerWidth, window.innerHeight);
			  renderer.setClearColor(0x111111, 1.0);
			  camera.position.set(0, 400, 600);
			  camera.lookAt(new THREE.Vector3());
			
			  scene.add(skyOctahedron.obj);
			  scene.add(skyOctahedronShell.obj);
			  on();
			  resizeWindow();
			  renderLoop();
			};
			init();
	    </script>
	</body>
</html>
